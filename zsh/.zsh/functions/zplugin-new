#autoload

local plugin_name="$1"
local package_dir="${2:-$DOTFILES_DIR/zsh}"

if [[ -z "$plugin_name" ]]; then
  print-error "Plugin name required"
  print "usage: ${funcstack[1]} NAME [DEST]"
  return 1
fi

local plugin_dir="$package_dir/.zsh/plugins/$plugin_name"
local zplugin_manifest_dir="$package_dir/.zsh/plugins/zplugin.d"
local zplugin_dir="$package_dir/.zplugin/plugins/_local---$plugin_name"
local plugin_file="$plugin_dir/init.zsh"
local zplugin_manifest_file="$zplugin_manifest_dir/$plugin_name.zplugin.zsh"
local zplugin_file="../../../../${package_dir:t}/.zsh/plugins/$plugin_name/init.zsh"

mkdir -vp "$plugin_dir" "$zplugin_manifest_dir" "$zplugin_dir"
touch "$plugin_file" "$zplugin_manifest_file"

print -l -- "# local plugins" "zplugin load _local/$plugin_name" >> "$zplugin_manifest_file"

(cd -q "$zplugin_dir" && ln -sfv "$zplugin_file" "$plugin_name.plugin.zsh")
