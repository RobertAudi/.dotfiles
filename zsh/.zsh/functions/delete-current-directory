#autoload

emulate -L zsh

setopt rm_star_silent rm_star_wait

local target_directory="${PWD:A}"

if [[ "$target_directory" == "${HOME:A}" ]]; then
  print-error "You can't delete your home directory: %B%F{yellow}${target_directory}%f%b"
  return 1
fi

if ! [[ "$target_directory" =~ "${HOME:A}" ]]; then
  print-error "You can't delete directories outside of your home directory: %B%F{yellow}${target_directory}%f%b"
  return 1
fi

builtin print -P -- "The following directory will be deleted: %B%F{yellow}${target_directory}%f%b"
builtin print -nP -- "%B%F{202}Are you sure you want to continue?%f%b (You have 5 seconds to answer) %B[y/n]%b"
builtin read -t5 -qs "? "

# This shit is required because read doesn't print a newline after swallowing the answer
local answer=$status ; builtin print

if [[ $answer -eq 0 ]]; then
  local parent_directory="${target_directory:h}"

  builtin cd -q "$parent_directory" || return $status

  { command rm -rfv "$target_directory"/{.,}*(N) && command rmdir "$target_directory" } || return $status

  if [[ $status -ne 0 ]]; then
    builtin cd -q "$target_directory"
    return 1
  fi

  if [[ -d "$target_directory" ]]; then
    print-error "Directory %U%Bnot%b%u deleted: %B%F{yellow}${target_directory}%f%b"
    builtin print -P -- "%B%F{196}How the fuck is that possible?\!?\!?\!%f%b" >&2
    builtin cd -q "$target_directory"
    return 1
  fi

  print-success "Directory deleted: %B%F{yellow}${target_directory}%f%b"
  builtin print -P -- "%F{245}You are now in: %F{yellow}%d%f"

  if [[ "${parent_directory:h}" != "${HOME:A}" && -n "$(find $parent_directory -type d -prune -empty)" ]]; then
    builtin print
    builtin print -P -- "The parent directory is empty: %B%F{yellow}${parent_directory}%f%b"
    builtin print -nP -- "%B%F{202}Do you want to delete it as well?%f%b (You have 5 seconds to answer) %B[y/n]%b"
    builtin read -t5 -qs "? "

    # This shit is required because read doesn't print a newline after swallowing the answer
    answer=$status ; builtin print

    if [[ $answer -eq 0 ]]; then
      builtin cd -q "${parent_directory:h}"

      { command rmdir "$parent_directory" } || return $status

      if [[ $status -ne 0 ]]; then
        builtin cd -q "$parent_directory"
        return 1
      fi

      if [[ -d "$parent_directory" ]]; then
        print-error "Directory %U%Bnot%b%u deleted: %B%F{yellow}${parent_directory}%f%b"
        builtin print -P -- "%B%F{196}How the fuck is that possible?\!?\!?\!%f%b" >&2
        builtin cd -q "$parent_directory"
        return 1
      fi

      print-success "Directory deleted: %B%F{yellow}${parent_directory}%f%b"
      builtin print -P -- "%F{245}You are now in: %F{yellow}%d%f"
    fi
  fi
else
  builtin print -P -- "Cancelled\! Directory %U%Bnot%b%u deleted: %B%F{yellow}${target_directory}%f%b" >&2
  return 1
fi
