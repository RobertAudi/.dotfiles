#autoload

if (( $# == 0 )); then
  builtin print -P -- "[%F{red}ERROR%f] tpane-git: not enough arguments" >&2
  return 1
fi

if ! type tmux >/dev/null || [[ -z $TMUX ]]; then
  git "$@"
  return $status
fi

zparseopts -D -horizontal+=horizontal h+=horizontal -vertical+=vertical v+=vertical

local cmd
if [[ -n "$1" ]]; then
  cmd="$1"; shift
fi

zparseopts -D -K -horizontal+=horizontal h+=horizontal -vertical+=vertical v+=vertical

if (( $#vertical == 0 && $#horizontal == 0 )) || [[ "$cmd" != "diff" && "$cmd" != "log" ]]; then
  git "$cmd" "$@"
  return $status
fi

local params
if (( $#vertical > 0 )); then
  params="-h -l 135"
else
  params="-v -p 50"
fi

if [[ "$cmd" == "diff" ]]; then
  cmd="${__gd_without_tpane__:-diff} --color $@ | diff-so-fancy | less -RX"
elif [[ "$cmd" == "log" ]]; then
  cmd="${__gl_without_tpane__:-log} --color $@ | less -RX"
else
  return 1
fi

tmux split-window $=params -- "git $cmd"
