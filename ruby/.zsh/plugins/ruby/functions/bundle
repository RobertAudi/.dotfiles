#autoload

case "$1" in
  "local")
    if [[ ! -f "Gemfile.local" ]]; then
      builtin print -P -u 2 -- "[%F{196}ERROR%f] Local Gemfile not found."
      return 1
    fi

    shift

    direnv_exec command bundle config --local gemfile "Gemfile.local"

    [[ -f "Gemfile.lock" ]] && cp -f "Gemfile.lock" "Gemfile.local.lock"

    typeset -gx BUNDLE_LOCAL_ENABLED=true

    ;;
  "unlocal")
    [[ -f ".bundle/config" ]] && sed -i "/BUNDLE_GEMFILE/d" ".bundle/config"
    [[ -f "Gemfile.local.lock" ]] && rm -f "Gemfile.local.lock"
    shift
    unset BUNDLE_LOCAL_ENABLED
    ;;
esac

if [[ -f "Gemfile.local.lock" ]]; then
  builtin print -P -- "[%F{032}INFO%f] %BUsing local Gemfile%b"
fi

if [[ -z "$1" || "$1" =~ "install|update" ]]; then
  if [[ -f "Gemfile.local.lock" ]]; then
    builtin print -P -- "[%F{032}INFO%f] Running %Bbundle $@%b using the regular Gemfile..."

    direnv_exec command bundle config --local gemfile "Gemfile"

    direnv_exec command bundle $@

    direnv_exec command bundle config --local gemfile "Gemfile.local"

    [[ -f "Gemfile.lock" ]] && cp -f "Gemfile.lock" "Gemfile.local.lock"
  fi
fi

direnv_exec command bundle "$@"
