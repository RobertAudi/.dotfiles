#autoload

emulate -L zsh

setopt rematch_pcre

# If arguments were given then just call `git push` normally
if (( $# > 0)); then
  git push "$@"

  return $status
fi

# Return early if `git push` succeeds
git push && return $status

local gitRemotes="$(git remote)"

if [[ -z "$gitRemotes" ]]; then
  builtin print -P -u 2 -- "[%F{196}ERROR%f] No tracked repository configured"

  return 128
elif ! [[ "$gitRemotes" =~ "\borigin\b" ]]; then
  builtin print -P -u 2 -- "[%F{196}ERROR%f] Remote not found: origin"
  builtin print    -u 2 -- "You can configure the 'origin' remote repository using"
  builtin print    -u 2 -- "\n\tgit remote add origin <url>"

  return 1
fi

root="$(git rev-parse --show-toplevel 2> /dev/null)"
branch="$(git current-branch)"

if ! git ls-remote --exit-code --heads "$root" "origin/${branch}"; then
  if read -q "?Push new branch to 'origin/${branch}'? [y/n] "; then
    git push --set-upstream origin "${branch}"
  fi
fi
