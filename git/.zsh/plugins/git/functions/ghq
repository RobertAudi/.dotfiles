#autoload

is-callable ghq || { print-error "ghq not installed"; return 1 }

# TODO: Fucking do something...
(($#)) || { command ghq help; return 1 }

local repo
local action="$1"; shift
case "$action" in
  "get")
    if (( $# == 0 )); then
      repo="$(pbpaste)"
      git ping "$repo" || return $status
      command ghq get --shallow --update "$repo"
    elif (( $# == 1 )); then
      command ghq get --shallow --update "$1"
    else # (( $# > 1 )) because $# can't be less than 0
      # NOTE: I use $@ instead of $1 to avoid false negatives if ghq options are passed
      command ghq get "${=@}"
    fi
    ;;
  "look")
    repos=($(ghq list "$1"))
    if ((${#repos})); then
      if (( ${#repos} == 1 )); then
        repo="$repos"
      elif type fzf >/dev/null; then
        repo="$(printf -- '%s\n' "${repos[@]}" | fzf)"
      else
        local -i repos_count=${#repos}

        if (( $repos_count > 3 )); then
          print-error "$repos_count repositories are found; Try more precise name."
        else
          print-error "More than one repositories are found; Try more precise name."
          for repo in ${repos[@]}; do
            printf '%10s- %s\n' "" "$repo"
          done
        fi

        return 1
      fi

      if (( $status == 130 )); then
        print-error "Aborted..."
        return 130
      fi

      repo="$(echo -n "$repo" | xargs echo -n)"
      if [[ -n "$repo" ]]; then
        local repo_url
        repo_url="${repo##${repo:h:h}/}"

        # Make sure to get the latest version
        ghq get --update "$repo_url"

        if [[ -n "$TMUX" && "$PWD" != "$HOME" ]]; then
          tmux new-window -c "$HOME" \; send-keys -l " cd \"$repo\"" \; send-keys "C-m" "C-l"
        else
          cd "$repo"
          if [[ -n "$TMUX" ]]; then
            tmux send-keys "C-l"
          else
            clear
          fi
        fi
      else
        print-error "Repo not found"
        return 1
      fi
    elif ghq get "$1"; then
      repo="$(ghq list "$1" | head -1)"
      if [[ -n "$TMUX" && "$PWD" != "$HOME" ]]; then
        tmux new-window -c "$HOME" \; send-keys -l " cd \"$repo\"" \; send-keys "C-m" "C-l"
      else
        cd "$repo"
        if [[ -n "$TMUX" ]]; then
          tmux send-keys "C-l"
        else
          clear
        fi
      fi
    else
      print-error "Repo not found"
      return 1
    fi
    ;;
  "list")
    query="$(echo -n "$1" | sed -E "s/^.*:|\.git\$|\/\$//g" | rev | cut -d/ -f-2 | rev)"
    command ghq list --full-path "$query"
    ;;
  *)
    command ghq "$action" "$@"
    ;;
esac

# vim: ft=zsh
