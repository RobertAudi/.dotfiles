#autoload

# cd-gitroot - zsh plugin to cd to git repository root directory
#
# Usage:
#   cd-gitroot [PATH]
#

cdgitroot_print_usage() {
  cat << EOF
Usage: cd-gitroot [PATH]
Change directory to current git repository root directory.
If PATH is specified, change directory to PATH instead of it.
PATH is treated relative path in git root directory.

  -h           display this help and exit
EOF
}

cdgitroot_print_error() {
  echo "cd-gitroot: $@" 1>&2
  echo "Try \`-h' option for more information." 1>&2
}

cdgitroot() {
  local option OPTARG OPTIND
  while getopts ':h' option; do
    case $option in
      h)
        cdgitroot_print_usage
        return 0
        ;;
      :)
        cdgitroot_print_error "option requires an argument -- $OPTARG"
        return 1
        ;;
      *)
        cdgitroot_print_error "invalid option -- $OPTARG"
        return 1
        ;;
    esac
  done
  shift $(expr $OPTIND - 1)

  if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) != 'true' ]]; then
    cdgitroot_print_error "Not in a git repository"
    return 1
  fi

  local relative_path="$1"
  local root_path=$(git rev-parse --show-toplevel)

  if [ -n "$relative_path" ]; then
    cd "$root_path/$relative_path"
  else
    cd "$root_path"
  fi
}

cdgitroot "$@"

# vim: ft=zsh
