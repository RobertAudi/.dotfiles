#!/usr/bin/env zsh

difftool() {
  emulate -L zsh
  zparseopts -D -E -A opts -local: -remote: -base:

  # All options are required
  if ! ((${+opts[--local]} && ${+opts[--remote]} && ${+opts[--base]})); then
    echo "Missing options" >&2
    return 1
  fi

  if [[ "$OSTYPE" =~ darwin* ]]; then
    # If Kaleidoscope and its command line tool are installed
    # Skip the first check if Kaleidoscope is already running
    local running=false ; pgrep "Kaleidoscope" >/dev/null && running=true
    if { $running || osascript -e "id of application \"Kaleidoscope\"" &>/dev/null } && type ksdiff >/dev/null; then
      $running || { open -g -a "Kaleidoscope" && sleep 1 }
      command ksdiff --partial-changeset --relative-path "${opts[--base]}" -- "${opts[--local]:A}" "${opts[--remote]:A}"
      return $?
    else
      echo "Kaleidoscope is not installed" >&2
      return 127
    fi
  elif [[ "$OSTYPE" =~ linux* ]]; then
    # TODO: Use meld or something
    echo "TODO: Use meld or something" >&2
    return 130
  fi

  echo "'$OSTYPE' not supported" >&2
  return 1
}

difftool $@
