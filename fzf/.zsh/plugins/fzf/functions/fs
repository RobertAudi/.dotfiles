#autoload

# fs [FUZZY PATTERN] - Select selected tmux session
#   - Bypass fuzzy finder if there's only one match (--select-1)
#   - Exit if there's no match (--exit-0)

local -a sessions
sessions=($(tmux list-sessions -F "#{session_name}"))

if (( ${#sessions} == 1 )); then
  if [[ -n "$TMUX" ]]; then
    builtin print -P -- "[%F{032}INFO%f] The active tmux session is the only one." >&2
    return 1
  else
    tmux attach-session -t "${session[1]}"
  fi
else
  local status_code
  local session
  session=$(builtin print -l -- "${sessions[@]}" | fzf --query="$1" --select-1 --exit-0)
  status_code=$?

  if (( $status_code != 0 )) || [[ -z "$session" ]]; then
    if (( $status_code == 130 )); then
      builtin print -P -- "[%F{196}ERROR%f] Aborted..." >&2
    elif (( $# > 0 )); then
      builtin print -P -- "[%F{196}ERROR%f] No session found" >&2
    fi

    (( $status_code == 0 )) && status_code=1

    return $status_code
  fi

  if [[ -n "$TMUX" ]]; then
    tmux switch-client -t "$session"
  else
    tmux attach-session -t "$session"
  fi
fi
